/*
 * Copyright (C) 2011-2020 Aestas/IT
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'idea'
    id 'java-library'
    id 'groovy'
    id 'org.kordamp.gradle.plugin'
    id 'org.ajoberstar.git-publish'
}

def resolveProperty(Project project, String envKey, String propKey, String defaultValue) {
    String value = System.getenv(envKey)
    if (!value) value = project.findProperty(propKey)
    !value? defaultValue : value
}

project.ext.'gradle.publish.key'    = resolveProperty(project, 'GRADLE_PUBLISH_KEY', 'gradle.publish.key', '**undefined**')
project.ext.'gradle.publish.secret' = resolveProperty(project, 'GRADLE_PUBLISH_SECRET', 'gradle.publish.secret', '**undefined**')

config {
    info {
        name          = 'Sshoogr Gradle Plugin'
        description   = 'A Gradle plugin for working with remote SSH servers'
        inceptionYear = '2011'
        tags          = ['ssh']

        specification {
            enabled = false
        }

        implementation {
            enabled = false
        }
    }

    docs {
        groovydoc {
            options {
                link 'https://docs.gradle.org/5.0/javadoc/', 'org.gradle.'
            }
        }
    }

    plugins {
        plugin {
            name                = project.name
            id                  = 'com.aestasit.sshoogr'
            implementationClass = 'com.aestasit.gradle.plugins.ssh.SshPlugin'
        }
    }
}

repositories {
    mavenLocal()
}

dependencies {
    compileOnly gradleApi()

    api "org.slf4j:slf4j-api:${slf4jVersion}"
    api "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    api "ch.qos.logback:logback-classic:${logbackVersion}"
    api("com.aestasit.infrastructure.sshoogr:sshoogr:${sshoogrVersion}") {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }

    testImplementation "com.aestasit.infrastructure.mock:groovy-sshd-mock:${sshdMockVersion}"
    testImplementation "commons-io:commons-io:${commonsIOVersion}"
    testImplementation "junit:junit:${junitVersion}"
}

gitPublish {
    repoUri = rootProject.extensions.findByName('config').info.links.scm
    branch = 'gh-pages'
    contents {
        from(groovydoc.outputs.files) {
            into 'docs/groovydoc'
        }
    }
}

gitPublishCommit.dependsOn(groovydoc)
