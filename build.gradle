/*
 * Copyright (C) 2011-2020 Aestas/IT
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id 'groovy'
  id 'com.aestasit.gradle.plugins.sshoogr-parentpom'
  id 'org.kordamp.gradle.plugin'
  id 'org.ajoberstar.git-publish'
}

config {
  info {
    name          = 'Sshoogr Gradle Plugin'
    description   = 'A Gradle plugin for working with remote SSH servers'
    inceptionYear = '2011'
    tags          = ['ssh']

    specification {
      enabled = false
    }

    implementation {
      enabled = false
    }
  }

  docs {
    groovydoc {
      options {
        link 'https://docs.gradle.org/5.0/javadoc/', 'org.gradle.'
      }
    }
  }

  plugin {
    enabled             = true
    id                  = 'com.aestasit.sshoogr'
    implementationClass = 'com.aestasit.gradle.plugins.ssh.SshPlugin'
  }

}

dependencies {
  compile gradleApi()
  compile "org.slf4j:slf4j-api:${slf4jVersion}"
  compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
  compile "ch.qos.logback:logback-classic:${logbackVersion}"
  compile("com.aestasit.infrastructure.sshoogr:sshoogr:${sshoogrVersion}") {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testCompile 'com.aestasit.infrastructure.mock:groovy-sshd-mock:0.3'
  testCompile 'commons-io:commons-io:2.6'
  testCompile 'junit:junit:4.13'
}

configurations {
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'log4j'
  all*.exclude module: 'slf4j-simple'
}

configurations.all {
  resolutionStrategy.force "org.slf4j:slf4j-api:${slf4jVersion}",
      "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
      "ch.qos.logback:logback-classic:${logbackVersion}",
      'commons-io:commons-io:2.6',
      'com.jcraft:jsch:0.1.51'
}

gitPublish {
  repoUri = rootProject.extensions.findByName('config').info.links.scm
  branch  = 'gh-pages'
  contents {
    from(groovydoc.outputs.files) {
      into 'docs/groovydoc'
    }
  }
}

gitPublishCommit.dependsOn(groovydoc)
